@ECHO OFF

ECHO "Starting Build for Client_Reposity..."
SETLOCAL
IF NOT DEFINED WORKDIR SET WORKDIR=.\
IF NOT DEFINED APP_ENV SET APP_ENV=NULL
pushd %WORKDIR%

REM #### Global Env settings ####
SET POWERGEN_ROOT=C:\PowerGen
SET BUILD_DIR=src\build

REM #### Application settings ####
SET BUILD_APP=client_app
SET PB_APP_NAME=client

REM #### Post app global settings ####
SET POWERGEN_ERR_FILE=pgerror_%BUILD_APP%.log
SET POWERGEN_GEN_FILE=pg_%BUILD_APP%.gen
SET POWERGEN_OLF_FILE=%BUILD_APP%.olf

REM remove log files...
DEL /S *.log

IF EXIST %BUILD_DIR%\client_app.pbl GOTO BUILD

:BOOTSTRAPIMPORT
ECHO BootStrap Import Application
start /Wait %POWERGEN_ROOT%\Pwrgn9.exe /J=%POWERGEN_GEN_FILE% %POWERGEN_OLF_FILE% /A=%PB_APP_NAME% /G /Q
IF NOT EXIST %POWERGEN_ERR_FILE% GOTO BUILD
ECHO Error in BootStrap Import Application
GOTO FAILEDBUILD

:BUILD
ECHO Building Application
start /D%WORKDIR% /Wait %POWERGEN_ROOT%\Pwrgn9.exe /A=%POWERGEN_GEN_FILE% /A=%PB_APP_NAME% /Q
IF NOT EXIST %POWERGEN_ERR_FILE% GOTO COPYMASTERS
ECHO Error Building Application
GOTO FAILEDBUILD

:COPYMASTERS
if NOT DEFINED ARTIFACTSDIR GOTO COMPLETEBUILD

:COPYARTIFACTS
ECHO Copying Build
xcopy /I/Y "%BUILD_DIR%\*.EXE" "%ARTIFACTSDIR%"
xcopy /I/Y "%BUILD_DIR%\*.PBD" "%ARTIFACTSDIR%"
xcopy /I/Y "%BUILD_DIR%\*.PBL" "%ARTIFACTSDIR%\pbls"
xcopy /I/Y *.log "%ARTIFACTSDIR%"
GOTO COMPLETEBUILD

:FAILEDBUILD
ECHO BUILD FAILED!
if DEFINED ARTIFACTSDIR xcopy /I/Y %WORKDIR%\*.log "%ARTIFACTSDIR%"
GOTO END

:COMPLETEBUILD
ECHO BUILD COMPLETED

:END
ENDLOCAL
popd
